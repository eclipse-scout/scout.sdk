/*
 * Copyright (c) 2010-2019 BSI Business Systems Integration AG.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     BSI Business Systems Integration AG - initial API and implementation
 */
import java.time.Clock
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'maven'
    id 'idea'
    id 'org.jetbrains.intellij' version '0.4.13'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
}

def SCOUT_SDK_VERSION = '10.0.0-SNAPSHOT'
def SCOUT_SDK_PLUGIN_VERSION = SCOUT_SDK_VERSION.replace('-SNAPSHOT', '.' + timestamp())

group 'org.eclipse.scout.sdk.s2i'
version SCOUT_SDK_VERSION
sourceCompatibility = 1.8


repositories {
    mavenLocal()
    mavenCentral()
}

static def timestamp() {
    LocalDateTime now = LocalDateTime.now(Clock.systemUTC())
    // returned number must be a valid integer (not too big)
    return now.format(DateTimeFormatter.ofPattern('yyMMddHHmm'))
}

dependencies {
    api group: 'org.eclipse.scout.sdk', name: 'org.eclipse.scout.sdk.core.s', version: SCOUT_SDK_VERSION
    api group: 'org.eclipse.scout.sdk', name: 'org.eclipse.scout.sdk.core.ecj', version: SCOUT_SDK_VERSION
    implementation group:'org.jetbrains.kotlin', name:'kotlin-stdlib-jdk8', version: '1.3.60'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.1.0'
}

jar {
    from 'about.html'
    from 'epl-v10.html'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2019.2.3'
    plugins 'java', 'maven', 'copyright'
    updateSinceUntilBuild = false
}

// Fix problems caused by separate output directories for classes/resources in IntelliJ IDEA
idea {
    module {
        inheritOutputDirs = true
    }
}

patchPluginXml {
    version SCOUT_SDK_PLUGIN_VERSION
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
