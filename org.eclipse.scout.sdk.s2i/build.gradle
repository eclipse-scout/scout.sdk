import java.time.Clock
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.9'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
}

group 'org.eclipse.scout.sdk.s2i'
version '10.0.0.' + timestamp()
sourceCompatibility = 1.8

def SCOUT_SDK_VERSION = '10.0.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

static def timestamp() {
    LocalDateTime now = LocalDateTime.now(Clock.systemUTC())
    return now.format(DateTimeFormatter.ofPattern("yyMMddHHmm")) // returned number must be a valid integer (not too big)
}

dependencies {
    api group: 'org.eclipse.scout.sdk', name: 'org.eclipse.scout.sdk.core.s', version: SCOUT_SDK_VERSION
    api group: 'org.eclipse.scout.sdk', name: 'org.eclipse.scout.sdk.core.ecj', version: SCOUT_SDK_VERSION
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.28.2'
}

jar {
    from 'about.html'
    from 'epl-v10.html'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2019.1.3'
    plugins 'maven'
    updateSinceUntilBuild = false
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
